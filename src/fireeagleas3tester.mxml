<?xml version="1.0" encoding="utf-8"?>
<!--
Copyright (c) 2009 Yahoo! Inc.  All rights reserved.  
The copyrights embodied in the content of this file are licensed under the BSD (revised) open source license
-->
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" height="100%" width="100%" initialize="init()" creationComplete="onCreationComplete()" horizontalAlign="center" verticalAlign="middle">

	<mx:Script>
		<![CDATA[
		
		import net.yahoo.fireeagle.*;
		import net.yahoo.fireeagle.realtime.*;
		import net.yahoo.fireeagle.data.*;
		import com.yahoo.oauth.OAuthSignatureMethod_PLAINTEXT; // only used if you switch from the default SHA1 signature method
		
		// init values for OAuth params
		[Bindable]
		private var _consumerKey:String = "";
		[Bindable]
		private var _consumerSecret:String = "";
		[Bindable]
		private var _generalKey:String = "";
		[Bindable]
		private var _generalSecret:String = "";
		[Bindable]
		private var _accessKey:String = "";
		[Bindable]
		private var _accessSecret:String = "";
		// xmpp credentials
		[Bindable]
		private var _jidXMPP:String = "";
		[Bindable]
		private var _passwordXMPP:String = "";
		
		// temp request token info
		[Bindable]
		private var _requestKey:String = "";
		[Bindable]
		private var _requestSecret:String = "";
		[Bindable]
		private var _authorizeUrl:String = "";
		// result pane bound var
		[Bindable]
		private var _result:String = "";
		
		// FE params
		[Bindable]
		private var _address:String = "";
		[Bindable]
		private var _postal:String = "";
		[Bindable]
		private var _city:String = "";
		[Bindable]
		private var _state:String = "";
		[Bindable]
		private var _country:String = "";
		[Bindable]
		private var _mnc:String = "";
		[Bindable]
		private var _mcc:String = "";
		[Bindable]
		private var _lac:String = "";
		[Bindable]
		private var _cellid:String = "";
		[Bindable]
		private var _lat:String = "";
		[Bindable]
		private var _lon:String = "";
		[Bindable]
		private var _woeid:String = "";
		[Bindable]
		private var _q:String = "";
		[Bindable]
		private var _time:String = "";
		[Bindable]
		private var _per_page:String = "";
		[Bindable]
		private var _page:String = "";
		
		// need to save auth object between calls
		private var _feAuth:FireEagleAuth = null;
		
		private var _realtime:IFireEagleSubscribe;
		
		private function init():void
		{
		}
		
		private function onCreationComplete():void
		{
		}
		
		//
		// FE Auth button click handlers
		//
		private function onNewRequest():void
		{
			_feAuth = new FireEagleAuth(_consumerKey, _consumerSecret);
			if (!sha1Radio.selected) {
				_feAuth.signatureMethod = new OAuthSignatureMethod_PLAINTEXT();
			}
			
			addListeners(_feAuth, FireEagleConfig.REQUEST_TOKEN_NAME, FireEagleEvent.REQUEST_TOKEN_SUCCESS, FireEagleEvent.REQUEST_TOKEN_FAILURE);

			_feAuth.addEventListener(FireEagleEvent.REQUEST_TOKEN_SUCCESS, function(e:FireEagleEvent):void{
				convertToAccessButton.visible = true;
				copyUrlToClickboardButton.visible = true;
				authorizeUrlLbl.visible = true;
				_authorizeUrl = _feAuth.authorizationUrl;
				_requestKey = _feAuth.requestKey;
				_requestSecret = _feAuth.requestSecret;
			});
			
			_feAuth.newRequestToken();
			
			updateResult(FireEagleConfig.REQUEST_TOKEN_NAME, _feAuth);
		}
		private function onCopyUrlToClipboard():void
		{
			System.setClipboard(_authorizeUrl);	
		}
		private function onConvertToAccess():void
		{
			addListeners(_feAuth, FireEagleConfig.ACCESS_TOKEN_NAME, FireEagleEvent.ACCESS_TOKEN_SUCCESS, FireEagleEvent.ACCESS_TOKEN_FAILURE);

			_feAuth.addEventListener(FireEagleEvent.ACCESS_TOKEN_SUCCESS, function(e:FireEagleEvent):void{
				_accessKey = _feAuth.accessKey;
				_accessSecret = _feAuth.accessSecret;
			});
			
			_feAuth.convertToAccessToken();
			
			updateResult(FireEagleConfig.ACCESS_TOKEN_NAME, _feAuth);
		}
		
		//
		// FE Methods button click handlers
		//
		private function onUser():void
		{
			var fe:FireEagleMethod = setUpRequest("user", FireEagleEvent.USER_SUCCESS, FireEagleEvent.USER_FAILURE);
			fe.user();
			updateResult("user", fe);
		}
		private function onLookup():void
		{
			var fe:FireEagleMethod = setUpRequest("lookup", FireEagleEvent.LOOKUP_SUCCESS, FireEagleEvent.LOOKUP_FAILURE);
			fe.lookup(buildArgs());
			updateResult("lookup", fe);
		}
		private function onUpdate():void
		{
			var fe:FireEagleMethod = setUpRequest("update", FireEagleEvent.UPDATE_SUCCESS, FireEagleEvent.UPDATE_FAILURE);
			fe.update(buildArgs());
			updateResult("update", fe);
		}
		private function onRecent():void
		{
			var fe:FireEagleMethod = setUpRequest("recent", FireEagleEvent.RECENT_SUCCESS, FireEagleEvent.RECENT_FAILURE, true, true);
			fe.recent(buildArgs());
			updateResult("recent", fe);
		}
		private function onWithin():void
		{
			var fe:FireEagleMethod = setUpRequest("within", FireEagleEvent.WITHIN_SUCCESS, FireEagleEvent.WITHIN_FAILURE, true, true);
			fe.within(buildArgs());
			updateResult("within", fe);
		}
		
		//
		// FE Methods url-gen button click handlers
		//
		private function onUserUrl():void
		{
			var fe:FireEagleMethod = setUpRequest("", "", "", false);
			fe.user();
			_result = formatResult("user", fe);
		}
		private function onLookupUrl():void
		{
			var fe:FireEagleMethod = setUpRequest("", "", "", false);
			fe.lookup(buildArgs());
			_result = formatResult("lookup", fe);
		}
		private function onUpdateUrl():void
		{
			var fe:FireEagleMethod = setUpRequest("", "", "", false);
			fe.update(buildArgs());
			_result = formatResult("update", fe);
		}
		private function onRecentUrl():void
		{
			var fe:FireEagleMethod = setUpRequest("", "", "", false, true);
			fe.recent(buildArgs());
			_result = formatResult("recent", fe);
		}
		private function onWithinUrl():void
		{
			var fe:FireEagleMethod = setUpRequest("", "", "", false, true);
			fe.within(buildArgs());
			_result = formatResult("within", fe);
		}
		
		//
		// FE poll
		//
		private function initRecentPoll():void
		{
			unInitXMPP();
			
			if (_realtime == null) {
				_realtime = new RecentPoller(_consumerKey, _consumerSecret, _generalKey, _generalSecret, 
					Number(pollTime.text)*1000, jsonRadio.selected ? JSONResponse : XMLResponse);
				_realtime.addEventListener(FireEagleEvent.RECENT_SUCCESS, function(e:FireEagleEvent):void{
					updateResponse("Recent Poll Success", e);
				});
				_realtime.addEventListener(FireEagleEvent.RECENT_FAILURE, function(e:FireEagleEvent):void{
					_result = "Recent Poll Failure" + "\n\n" + dumpObject(e.data);
				});
				_realtime.addEventListener(FireEagleEvent.USER_SUCCESS, function(e:FireEagleEvent):void{
					updateResponse("User Poll Success", e);
				});
				_realtime.addEventListener(FireEagleEvent.USER_FAILURE, function(e:FireEagleEvent):void{
					_result = "User Poll Failure" + "\n\n" + dumpObject(e.data);
				});
			}
		}
		private function unInitRecentPoll():void
		{
			if (_realtime != null && _realtime is RecentPoller) {
				_realtime.stop();
				recentPollButton.label = "recent";
				_realtime = null;
			}
		}
		private function onRecentPoll():void
		{
			initRecentPoll();
			
			if (_realtime.running) {
				_realtime.stop();
				recentPollButton.label = "recent";
			} else {
				_realtime.start();
				recentPollButton.label = "stop recent";
				_result = "polling recent";
			}
		}
		private function onSubscribePoll():void
		{
			initRecentPoll();
			
			_realtime.subscribe(_accessKey, _accessSecret);
		}
		//
		// FE XMPP
		//
		private function initXMPP():void
		{
			unInitRecentPoll();
			
			if (_realtime == null) {
				_realtime = new XMPPConnection(_jidXMPP, _passwordXMPP, null, _consumerKey, _consumerSecret);
				var f:Function = function(e:RealTimeEvent):void{
					_result += "\n" + e.type;
				};
				_realtime.addEventListener(RealTimeEvent.XMPP_AUTH_FAIL, f);
				_realtime.addEventListener(RealTimeEvent.XMPP_AUTH_SUCCESS, f);
				_realtime.addEventListener(RealTimeEvent.XMPP_CONNECT, f);
				_realtime.addEventListener(RealTimeEvent.XMPP_CONNECT_FAIL, f);
				_realtime.addEventListener(RealTimeEvent.XMPP_DISCONNECT, f);
				_realtime.addEventListener(RealTimeEvent.XMPP_SECURE, f);
				_realtime.addEventListener(RealTimeEvent.XMPP_SESSION, f);
				
				_realtime.addEventListener(FireEagleEvent.USER_SUCCESS, function(e:FireEagleEvent):void{
					updateResponse("User XMPP Update received", e);
				});
			}
		}
		private function unInitXMPP():void
		{
			if (_realtime != null && _realtime is XMPPConnection) {
				_realtime.stop();
				connectXMPPButton.label = "connect";
				_realtime = null;
			}
		}
		private function onConnectXMPP():void
		{
			initXMPP();
			
			if (_realtime.running) {
				_realtime.stop();
				connectXMPPButton.label = "connect";
			} else {
				_realtime.start();
				connectXMPPButton.label = "disconnect";
				_result = "XMPP connect";
			}
		}
		private function onSubscribeXMPP():void
		{
			initXMPP();
			
			_realtime.subscribe(_accessKey, _accessSecret);
		}
		
		//
		// response formatting helper
		// 
		private function updateResult(text:String, fe:FireEagleMethod):void
		{
			_result = "requesting "+ formatResult(text, fe);
		}
		private function formatResult(text:String, fe:FireEagleMethod):String
		{
			var c:String = text;
			if (fe.url.length) {
				c += " url:\n" + fe.url;
			}
			if (fe.args.length) {
				c += "\n\nargs:\n"+fe.args;
			}
			if (getCurlCommand(fe).length) {
				c += "\n\ncurl:\n" + getCurlCommand(fe);
			}
			return c;
		}
		private function getCurlCommand(fe:FireEagleMethod):String {
			var c:String = "";
			if (fe.url.length) {
				c = "curl ";
				if (fe.args.length) {
					c += "-F '"+fe.args+"' ";
				}
				c += "'"+fe.url+"'";
			}
			return c;
		}
		private function formatWithLinebreaks(s:String):String
		{
			return s.split(",").join(",\n");
		}
		private function updateResponse(text:String, e:FireEagleEvent):void
		{
			_result = text + formatResponse(e);
		}
		private function formatResponse(e:FireEagleEvent):String
		{
			var s:String = "";
				if (e.response != null) {
					s += "\n\n"+formatWithLinebreaks(e.response.toString());
				}
				s += "\n\nresponse("+new Date().toString()+"):\n" + dumpObject(e.data);
				return s;
		}
		
		//
		// FE method call setup helpers
		//
		private function setUpRequest(text:String, success:String="", failure:String="", fireRequest:Boolean=true, generalKey:Boolean=false):FireEagleMethod
		{
			var fe:FireEagleMethod = 
				new FireEagleMethod(_consumerKey, _consumerSecret, 
					generalKey ? _generalKey : _accessKey, 
					generalKey ? _generalSecret : _accessSecret, 
					jsonRadio.selected ? JSONResponse : XMLResponse);
			fe.fireRequestAutomatically = fireRequest;
			if (!sha1Radio.selected) {
				fe.signatureMethod = new OAuthSignatureMethod_PLAINTEXT();
			}
			
			addListeners(fe, text, success, failure);

			return fe;
		}
		private function addListeners(fe:FireEagleMethod, text:String, success:String="", failure:String=""):void
		{
			if (success.length) {
				fe.addEventListener(success, function(e:FireEagleEvent):void{
					_result = formatResult(text + " Success", fe) + formatResponse(e);
				});
			}
			if (failure.length) {
				fe.addEventListener(failure, function(e:FireEagleEvent):void{
					_result = formatResult(text + " Failure", fe) + "\n\n" + dumpObject(e.data);
				});
			}
		}
		private function buildArgs():Object
		{
			var args:Object = new Object();
			if (_address.length || _postal.length || _city.length || _state.length || _country.length) {
				if (_address.length) {
					args['address'] = _address;
				}
				if (_postal.length) {
					args['postal'] = _postal;
				}
				if (_city.length) {
					args['city'] = _city;
				}
				if (_state.length) {
					args['state'] = _state;
				}
				if (_country.length) {
					args['country'] = _country;
				}
			} else if (_mnc.length || _mcc.length || _lac.length || _cellid.length) {
				args['mnc'] = _mnc;
				args['mcc'] = _mcc;
				args['lac'] = _lac;
				args['cellid'] = _cellid;
			} else if (_lat.length || _lon.length) {
				args['lat'] = _lat;
				args['lon'] = _lon;
			} else if (_woeid.length) {
				args['woeid'] = _woeid;
			} else if (_q.length) {
				args['q'] = _q;
			}
			if (_time.length) {
				args['time'] = _time;
			}
			if (_per_page.length) {
				args['per_page'] = _per_page;
			}
			if (_page.length) {
				args['page'] = _page;
			}
			return args;
		}
		
		//
		// output format helper
		//
		private function dumpObject(o:Object, depth:String=""):String
		{
			if (o is String || o is Number || o is XML) {
				return depth + o.toString() + "\n";
			}
			var s:String = "";
			for (var i:String in o) {
				if ((o[i] is Object || o[i] is Array) && !(o[i] is String || o[i] is Number || o[i] is XML)) {
					s += depth + i + "=\n";
					s += dumpObject(o[i], depth + "  ");
				} else {
					s += depth + i + "=" + o[i] + "\n";
				}
			}
			return s;
		}
		
		]]>
	</mx:Script>
		
	<mx:HBox width="100%" height="100%">
		<mx:VBox width="100%" height="100%">
			<mx:Label text="Fire Method:" />
			<mx:HBox>
				<mx:Button id="userButton" label="user" click="onUser()" />
				<mx:Button id="lookupButton" label="lookup" click="onLookup()" />
				<mx:Button id="updateButton" label="update" click="onUpdate()" />
				<mx:Button id="recentButton" label="recent" click="onRecent()" />
				<mx:Button id="withinButton" label="within" click="onWithin()" />
			</mx:HBox>
			<mx:Label text="Get URL and curl command only:" />
			<mx:HBox>
				<mx:Button id="userUrlButton" label="user" click="onUserUrl()" />
				<mx:Button id="lookupUrlButton" label="lookup" click="onLookupUrl()" />
				<mx:Button id="updateUrlButton" label="update" click="onUpdateUrl()" />
				<mx:Button id="recentUrlButton" label="recent" click="onRecentUrl()" />
				<mx:Button id="withinUrlButton" label="within" click="onWithinUrl()" />
			</mx:HBox>
			<mx:Label text="Start Polling:" />
			<mx:HBox>
				<mx:Button id="recentPollButton" label="recent" click="onRecentPoll()" />
				<mx:Button id="subscribePollButton" label="add user" click="onSubscribePoll()" />
				<mx:Label text="Delay(seconds):" />
				<mx:TextInput id="pollTime" restrict="0123456789" text="10" />
			</mx:HBox>
			<mx:Label text="Start XMPP:" />
			<mx:HBox>
				<mx:Button id="connectXMPPButton" label="connect" click="onConnectXMPP()" />
				<!--<mx:Button id="subscribeXMPPButton" label="subscribe user" click="onSubscribeXMPP()" />-->
				<mx:Label text="Jid:" />
				<mx:TextInput id="jidXMPP" text="{_jidXMPP}"/>
				<mx:Label text="Password:"/>
				<mx:TextInput id="passwordXMPP" displayAsPassword='true' text="{_passwordXMPP}"/>
			</mx:HBox>
			
			<mx:Spacer height="5"/>
			<mx:Label text="OAuth:" />
			<mx:Panel headerHeight="10" horizontalCenter="0" >
				<mx:HBox>
					<mx:VBox>
						<mx:Label text="consumerKey" />
						<mx:TextInput id="consumerKeyTxt" text="{_consumerKey}" valueCommit="{_consumerKey = consumerKeyTxt.text;}"/>
					</mx:VBox>
					<mx:VBox>
						<mx:Label text="consumerSecret" />
						<mx:TextInput id="consumerSecretTxt" text="{_consumerSecret}" valueCommit="{_consumerSecret = consumerSecretTxt.text;}"/>
					</mx:VBox>
				</mx:HBox>
				<mx:HBox>
					<mx:VBox>
						<mx:Label text="generalKey" />
						<mx:TextInput id="generalKeyTxt" text="{_generalKey}" valueCommit="{_generalKey = generalKeyTxt.text;}"/>
					</mx:VBox>
					<mx:VBox>
						<mx:Label text="generalSecret" />
						<mx:TextInput id="generalSecretTxt" text="{_generalSecret}" valueCommit="{_generalSecret = generalSecretTxt.text;}"/>
					</mx:VBox>
				</mx:HBox>
				<mx:HBox>
					<mx:VBox>
						<mx:Label text="accessKey" />
						<mx:TextInput id="accessKeyTxt" text="{_accessKey}" valueCommit="{_accessKey = accessKeyTxt.text;}"/>
					</mx:VBox>
					<mx:VBox>
						<mx:Label text="accessSecret" />
						<mx:TextInput id="accessSecretTxt" text="{_accessSecret}" valueCommit="{_accessSecret = accessSecretTxt.text;}"/>
					</mx:VBox>
				</mx:HBox>
				<mx:VBox>
					<mx:HBox>
						<mx:Button id="newRequestButton" label="Get New Request Token" click="onNewRequest()" />
						<mx:Button id="copyUrlToClickboardButton" label="Copy Url to Clipboard" click="onCopyUrlToClipboard()" visible="false" /> 
						<mx:Button id="convertToAccessButton" label="Convert to Access Token" click="onConvertToAccess()" visible="false" toolTip="{'request_key='+_requestKey+'\nrequest_secret='+_requestSecret}"/>
					</mx:HBox>
					<mx:Label id="authorizeUrlLbl" visible="false" text="{_authorizeUrl}" selectable="true" />
				</mx:VBox>
			</mx:Panel>
			
			<mx:Spacer height="5"/>
			<mx:HBox>
			<mx:VBox>
				<mx:Label text="Result Format:" />
				<mx:HBox>
					<mx:RadioButton id="jsonRadio" label="JSON" groupName="resultformat"/>
					<mx:RadioButton label="XML" selected="true" groupName="resultformat"/>
				</mx:HBox>
			</mx:VBox>
			<mx:VBox>
				<mx:Label text="Signature Method:" />
				<mx:HBox>
					<mx:RadioButton id="sha1Radio" label="SHA1" selected="true" groupName="signaturemethod"/>
					<mx:RadioButton label="Plain Text" groupName="signaturemethod"/>
				</mx:HBox>
			</mx:VBox>
			</mx:HBox>
			
			<mx:Spacer height="5"/>
			<mx:Label text="Parameters:" />
			<mx:Panel headerHeight="10" horizontalCenter="0" >
				<mx:HBox>
					<mx:VBox>
						<mx:Label text="address" toolTip="street address (may contain a full address, but will be combined with postal, city, state, and country when available)" />
						<mx:TextInput id="addressTxt" text="{_address}" valueCommit="{_address = addressTxt.text;}"/>
					</mx:VBox>
					<mx:VBox>
						<mx:Label text="postal" toolTip="a ZIP or postal code (combined with address, city, state, and country when available)" />
						<mx:TextInput id="postalTxt" text="{_postal}" valueCommit="{_postal = postalTxt.text;}"/>
					</mx:VBox>
					<mx:VBox>
						<mx:Label text="city" toolTip="city (combined with address, postal, state, and country when available)" />
						<mx:TextInput id="cityTxt" text="{_city}" valueCommit="{_city = cityTxt.text;}"/>
					</mx:VBox>
				</mx:HBox>
				<mx:HBox>
					<mx:VBox>
						<mx:Label text="state" toolTip="state (combined with address, postal, city, and country when available)" />
						<mx:TextInput id="stateTxt" text="{_state}" valueCommit="{_state = stateTxt.text;}"/>
					</mx:VBox>
					<mx:VBox>
						<mx:Label text="country" toolTip="country (combined with address, postal, city, and state when available)" />
						<mx:TextInput id="countryTxt" text="{_country}" valueCommit="{_country = countryTxt.text;}"/>
					</mx:VBox>
				</mx:HBox>
			</mx:Panel>
			<mx:Panel headerHeight="10" horizontalCenter="0" >
				<mx:HBox>
					<mx:VBox>
						<mx:Label text="mnc" toolTip="cell tower information, integer." />
						<mx:TextInput id="mncTxt" text="{_mnc}" valueCommit="{_mnc = mncTxt.text;}"/>
					</mx:VBox>
					<mx:VBox>
						<mx:Label text="mcc" toolTip="cell tower information, integer." />
						<mx:TextInput id="mccTxt" text="{_mcc}" valueCommit="{_mcc = mccTxt.text;}"/>
					</mx:VBox>
					<mx:VBox>
						<mx:Label text="lac" toolTip="cell tower information, integer." />
						<mx:TextInput id="lacTxt" text="{_lac}" valueCommit="{_lac = lacTxt.text;}"/>
					</mx:VBox>
					<mx:VBox>
						<mx:Label text="cellid" toolTip="cell tower information, integer." />
						<mx:TextInput id="cellidTxt" text="{_cellid}" valueCommit="{_cellid = cellidTxt.text;}"/>
					</mx:VBox>
				</mx:HBox>
			</mx:Panel>
			<mx:HBox>	
				<mx:Panel headerHeight="10" horizontalCenter="0" >
					<mx:HBox>
						<mx:VBox>
							<mx:Label text="lat" toolTip="Latitude (in decimal degrees)." />
							<mx:TextInput id="latTxt" text="{_lat}" valueCommit="{_lat = latTxt.text;}"/>
						</mx:VBox>
						<mx:VBox>
							<mx:Label text="lon" toolTip="Longitude (in decimal degrees)." />
							<mx:TextInput id="lonTxt" text="{_lon}" valueCommit="{_lon = lonTxt.text;}"/>
						</mx:VBox>
					</mx:HBox>
				</mx:Panel>
				<mx:Panel headerHeight="10" horizontalCenter="0" >
					<mx:HBox>
						<mx:VBox>
							<mx:Label text="woeid" toolTip="Where On Earth ID" />
							<mx:TextInput id="woeidTxt" text="{_woeid}" valueCommit="{_woeid = woeidTxt.text;}"/>
						</mx:VBox>
					</mx:HBox>
				</mx:Panel>
				<mx:Panel headerHeight="10" horizontalCenter="0" >
					<mx:HBox>
						<mx:VBox>
							<mx:Label text="q" toolTip="Free-text fallback containing user input. Lat/lon pairs and geometries will be extracted if possible, otherwise this string will be geocoded as-is." />
							<mx:TextInput id="qTxt" text="{_q}" valueCommit="{_q = qTxt.text;}"/>
						</mx:VBox>
					</mx:HBox>
				</mx:Panel>
			</mx:HBox>
			<mx:Panel headerHeight="10" horizontalCenter="0" >
				<mx:HBox>
					<mx:VBox>
						<mx:Label text="time" toolTip="(default: now) : the time to start looking at recent updates from." />
						<mx:TextInput id="timeTxt" text="{_time}" valueCommit="{_time = timeTxt.text;}"/>
					</mx:VBox>
					<mx:VBox>
						<mx:Label text="per_page" toolTip="(default: 10) : number of users to return per page." />
						<mx:TextInput id="per_pageTxt" text="{_per_page}" valueCommit="{_per_page = per_pageTxt.text;}"/>
					</mx:VBox>
					<mx:VBox>
						<mx:Label text="page" toolTip="(default: 0) : the page number at which to start returning the list of users, pages are 0-indexed, each page contains the per_page number of users." />
						<mx:TextInput id="pageTxt" text="{_page}" valueCommit="{_page = pageTxt.text;}"/>
					</mx:VBox>
				</mx:HBox>
			</mx:Panel>
		</mx:VBox>
		
		<mx:VBox width="100%" height="100%">
			<mx:Label text="Result:" />
			<mx:TextArea id="resultTxt" text="{_result}" wordWrap="false" width="90%" height="90%" />
		</mx:VBox>
	</mx:HBox>		
	
</mx:Application>
